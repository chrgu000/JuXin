绿低量:=10;
{绿低量等于数字 10}
蓝低量:=50;
{蓝低量等于数字 50}
灰低量:=100;
{灰低量等于数字 100}
H_L:=0.1*CONST(HHV(VOL,240));
{最近240个周期内最大成交量 乘以0.1}
VVOL:=IF(CURRBARSCOUNT=1 AND PERIOD=5,VOL*240/FROMOPEN,DRAWNULL);
{VVOL等于    当前周期是日线，取最后一个周期，现在的成交量 乘以240 除以 开盘到现在的分钟数}
STICKLINE(CURRBARSCOUNT=1 AND PERIOD=5,VVOL,0,1,-1),COLOR00C0C0;
{当前周期是日线，在最后一个K线成交量上，绘制以VVOL为高度，绘制黄色柱线}
量:VOL,VOLSTICK,COLORLIGRAY;
{绘制成交量红蓝柱子}
换手: VOL*10000/FINANCE(7),NODRAW,COLORGREEN;
{换手等于  成交量 乘以10000 除以 流通股本} 
全换手周期: SUMBARS(V,CAPITAL),NODRAW,COLORLIGREEN;
{全换手周期等于  多少周期成交量向前累加大于等于 当前流通股本}
量比: V/REF(MA(V,5),1),COLORBROWN,NODRAW;
{量比等于，  成交量 除以 过去5个周期的平均数值}
几倍: V/REF(V,1),COLORYELLOW,NODRAW;
{几倍等于   当前周期的成交量 除以 前一个周期的成交量}
倍数:=1.9;
{倍数等于  1.9}
倍量: VOL>=REF(V,1)*倍数 AND (IF(C<O,C>REF(C,1),1)),COLORYELLOW,NODRAW;
{倍量等于   成交量大于等于 前一个周期的成交量乘以1.9  同时  收盘大于开盘。 或是 收盘大于前一个周期收盘 }
倍缩: REF(V,1)>=VOL*倍数,COLORMAGENTA,NODRAW;
{倍缩 等于   前一个周期成交量大于 当前周期成交量 乘以1.8}
平量: RANGE(V/REF(V,1),0.97,1.03) AND BARSCOUNT(V)>=2,COLORWHITE,NODRAW;
{平量等于 成交量在前一个周期成交量的上下3%之内}
低量10:VOL=LLV(VOL,绿低量) AND BARSCOUNT(V)>=绿低量,COLORGREEN,NODRAW;
{低量10等于   当前成交量是最近10个周期的最低成交量}
低量50:VOL=LLV(VOL,蓝低量) AND BARSCOUNT(V)>=蓝低量,COLORBLUE,NODRAW;
{低量50等于   当前成交量是最近50个周期的最低成交量}
地量:VOL=LLV(VOL,灰低量) AND BARSCOUNT(V)>=灰低量,COLORGRAY,NODRAW;
{地量等于  当前成交量是最近100个周期的最低成交量}
V1:=VOL*0.95;
{V1等于  当前成交量乘以0.95}
V2:=VOL*0.85;
{V2等于  当前成交量乘以0.85}
STICKLINE(倍量, 0,V1,1,0),COLORYELLOW;
{倍量条件成立，以V1高度绘制黄色成交量柱子}
STICKLINE(平量,0,V1,1,0),COLORWHITE;
{平量条件成立，以V1高度绘制白色成交量柱子}
STICKLINE(低量10,0,V2,1,0),COLORLIGREEN;
{低量10条件成立，以V2高度绘制浅绿色成交量柱子}
STICKLINE(低量50,0,V2,1,0),COLORBLUE;
{低量50条件成立，以V2高度绘制蓝色成交量柱子}
STICKLINE(地量,0,V2,1,0),COLORGRAY;
{地量条件成立，以V2高度绘制灰色成交量柱子}
STICKLINE(倍缩,0,V*0.45,1,0),COLORMAGENTA;
{倍缩条件成立，以成交量*0.45 为高度绘制粉色成交量柱子}
STICKLINE(C<O AND C>REF(C,1),0,V1,0,0),COLORRED;
{收盘小于开盘 同时收盘大于前一个周期的收盘，以成交量为高度，绘制红色柱子}
真阳或假阴:=C>O OR C>REF(C,1);
{真阳或假阴等于    收盘大于开盘  或是  收盘大于前一个周期的收盘}
收阳:= REF(真阳或假阴,3);
{收阳等于，  倒数第4个K线是 真阳或假阴 }
倍量1:=REF(倍量,3);
{倍量1等于   倒数第4个K线是 倍量} 
高量:=REF(V,3)>=HHV(V,7);
{高量等于   倒数第4个K线的成交量 大于等于 最近7个成交量最大数值}
三日不破:= LLV(C,3)>=REF(L,3);
{三日不破等于  最近3个周期的最低收盘价格 大于等于 倒数第4个周期的最低价格}
价升:=COUNT(C>=REF(C,1),3)=3;
{价升等于   连续3个周期  收盘大于前一个周期的收盘。   就是3连阳}
量缩:=COUNT(V<=REF(V,1),3)=3;
{量缩等于，  连续3个周期缩量 ，连续三个周期成交量小于前一个周期的成交量}
连阳:= COUNT(真阳或假阴,3)=3;
{连阳等于，  连续3个周期  是 真阳或假阴 }
倍量将军柱:= 倍量1 AND 三日不破 AND 收阳;
{倍量将军柱等于     倍量1 成立 同时三日不破 成立 同时 收阳 }
高量将军柱:= 高量  AND 三日不破 AND 收阳;
{高量将军柱等于  高量成立  同时 三日不破成立  同时 收阳成立}
将军柱:= 倍量将军柱 OR 高量将军柱;
{将军柱等于  倍量将军柱成立  或是 高量将军柱成立}
黄金柱:= 将军柱 AND 价升 AND 量缩 AND 连阳 ;
{黄金柱等于  将军柱成立  同时 价升成立  同时 量缩成立  同时 连阳成立  }
STICKLINE(黄金柱,0.3*V,0.7*V,1,0),COLORRED;
{黄金柱成立   在成交量*0.3 到成交量乘以0.7的位置， 绘制红色柱子 } 
DRAWICON(黄金柱,V*1.5,1);
{黄金柱成立 ，出现红色箭头}
V100:= V<=LLV(V,100)*1.2 AND V>0 AND BARSCOUNT(V)>=100;
{V100等于    当前成交量小于等于 最近100个周期最低成交量乘以1.2}
百日地量群:=COUNT(V100,5)>=3;
{百日地量群等于    最近5个周期，有3个周期，都符合 V100的条件}
DRAWTEXT(FILTER(百日地量群,4),V+3*H_L,'地量群'),COLORGRAY;
{百日地量群条件成立，在成交量加上 3乘以H_L的地方  显示 文字 '地量群'，4个周期内，再次符合百日地量群条件不显示文字  }
长阴短柱:=C/REF(C,1)<=0.96 AND V<HHV(V,30)/1.9;
{长阴短柱等于 成交量除以前一个成交量 小于等于0.96  同时 成交量小于 30个周期最大成交量除以1.9}
DRAWTEXT(长阴短柱 ,V+1.6*H_L,'！'),COLORLICYAN;
{长阴短柱条件成立，在成交量加上 1.6乘以H_L的地方 显示一个感叹号}